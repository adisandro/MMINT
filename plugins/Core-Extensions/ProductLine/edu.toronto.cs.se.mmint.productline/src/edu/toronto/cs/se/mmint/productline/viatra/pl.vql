/*******************************************************************************
 * Copyright (c) 2021, 2024 Alessio Di Sandro.
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * https://www.eclipse.org/legal/epl-2.0/
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     Alessio Di Sandro - Implementation
 *******************************************************************************/
package edu.toronto.cs.se.mmint.productline.viatra

import "model://edu.toronto.cs.se.mmint.productline"
import "http://www.eclipse.org/emf/2002/Ecore"

private pattern superType(class: EClass, superClass: EClass) {
  class != superClass;
  EClass.eSuperTypes(class, superClass);
}

pattern classType(plClass: Class, type: java String) {
  Class.type(plClass, class);
  find superType*(class, superClass);
  EClass.name(superClass, type);
}

pattern reference(plSrc: Class, srcType: java String, plDst: Class, dstType: java String,
                  plRef: Reference, refType: java String) {
  find classType(plSrc, srcType);
  Class.references(plSrc, plRef);
  Reference.type.name(plRef, refType);
  Reference.target(plRef, plDst);
  find classType(plDst, dstType);
}

pattern attribute(plElem: Class, elemType: java String,
                  plAttr: Attribute, attrType: java String, attrValue: java String) {
  find classType(plElem, elemType);
  Class.attributes(plElem, plAttr);
  Attribute.type.name(plAttr, attrType);
  Attribute.value(plAttr, attrValue);
}
